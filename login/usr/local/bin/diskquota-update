#! /usr/bin/zsh

if (( $@[(Ie)--help] )); then
	echo '
sudo diskquota update

Read the limit generated by `diskquota-limit` and update btrfs.
'
	exit
fi

# disallow undefined variables
set -u


function createQgroup() {
	user=$1
	uid=$2

	btrfs qgroup create 1/$uid /home/$user

	baseId=$(btrfs subvolume show /home/$user | grep 'Quota group' | grep -o -P '0/\d+')
	sharedId=$(btrfs subvolume show /home/shared/$user | grep 'Quota group' | grep -o -P '0/\d+')

	btrfs qgroup assign --no-rescan $baseId 1/$uid /home
	btrfs qgroup assign --no-rescan $sharedId 1/$uid /home
}



condition=$(diskquota condition)
if [[ $condition == 'enable' ]]; then
	echo 'enable disk quota'
	if [[ $(btrfs qgroup show /home 2>&1) == *"quotas not enabled"* ]]; then
		# quotas not enabled
		touch /etc/diskquota/enable
		btrfs quota enable /home

		# Refer to the limit in cluster-add-user/adduser.conf
		while IFS=: read user pw uid other; do
			if (( uid >=5000 && uid <= 59999 )); then
				createQgroup $user $uid
			fi
		done < /etc/passwd
	fi
elif [[ $condition == 'disable' ]]; then
	echo 'disable disk quota'
	rm -f /etc/diskquota/enable
	btrfs quota disable /home
	exit
fi

limitInB=$(( [#10] ($(diskquota limit) * 1024) ))

# Remove exceptions made 90 days ago.
find /etc/diskquota/exemptions/ -type f  -mtime +90 -delete

for id in $(awk -F: '$3>=5000 && $3<=59999 { print $3}' /etc/passwd)
do
	# echo "Update $id quota to $limitInB"
	if diskquota exempt check $id>/dev/null ; then
		# echo $id $(id -n -u $id) "is exempted"
		btrfs qgroup limit none 1/$id /home
	else
		btrfs qgroup limit $limitInB 1/$id /home
	fi
done

