#! /usr/bin/zsh


if (($@[(Ie)--help])) then
	echo "sudo diskquota limit [Options]

Get or set disk quota limit. The limit is updated every 7 days.
This tool provides no way to force update the limit.

The program throws error when disk quota is not enabled.

Options
--dryrun	Ignore cache and calculate limit
-h	Output limit in human readable unit
--help	Output this help
"
	exit
fi


if (( EUID == 0 )); then
	# If I write `find /etc/diskquota/limit`
	# find may report error: No such file or directory
	sudo find /etc/diskquota -name limit -type f  -mtime +7 -delete
fi


if (($@[(Ie)--dryrun]==0)) && [[ -s /etc/diskquota/limit ]]; then
	limitKB=$(cat /etc/diskquota/limit)
else
	local sizes=()
	local exemptedUsers=0
	while read folder size _; do 
		user=${${folder#<}%-total>}
		if diskquota exempt check $user > /dev/null; then
			(( exemptedUsers+=1 ))
		else
			sizes+=($size)
		fi
	done <<< $(diskquota show --all --total --raw| tail -n +2)

	sizes=( ${(nO)sizes} )
	repeat $exemptedUsers; sizes+=($sizes[1])

	# The first "-" means to read from stdin.
	# The second "-" means to output to stdout instead of stderr.
	limitKB=$(printf '%s\n' $sizes | 
	gnuplot -e 'stats "-" nooutput; set print "-"; print (STATS_mean+2*STATS_stddev)/1024')
	# The integer in gnuplot version 5.2 is 32 bits, so we cannot use 
	# int((STATS_mean+2*STATS_stddev)/1024) 
	# because overflow is likely to occur.
	# Hence, we use printf.
	limitKB=$(printf '%.0f\n' $limitKB)

	if (($@[(Ie)--dryrun]==0)); then
		echo $limitKB > /etc/diskquota/limit
	fi
fi

if (($@[(Ie)-h])) then
	printf "%.2fGB\n" $((limitKB/1024.0/1024))
else
	echo $limitKB
fi

